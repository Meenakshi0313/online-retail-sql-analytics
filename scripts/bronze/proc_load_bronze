/*
===============================================================================
Stored Procedure: Load Bronze Layer (Source -> Bronze)
===============================================================================
Script Purpose:
    This stored procedure loads data into the 'bronze.online_retail' table
    from an external CSV file. It performs the following actions:
    - Truncates the bronze table before loading.
    - Uses BULK INSERT to load CSV data into the raw bronze table.

Parameters:
    None.

Usage Example:
    EXEC bronze.proc_load_bronze_online_retail;
===============================================================================
*/

CREATE OR ALTER PROCEDURE bronze.proc_load_bronze_online_retail
AS
BEGIN
    DECLARE @start_time DATETIME, @end_time DATETIME;

    BEGIN TRY
        PRINT '================================================';
        PRINT 'Loading Bronze Layer - Online Retail';
        PRINT '================================================';

        SET @start_time = GETDATE();

        PRINT '>> Truncating Table: bronze.online_retail';
        TRUNCATE TABLE bronze.online_retail;

        PRINT '>> Inserting Data Into: bronze.online_retail';
        BULK INSERT bronze.online_retail
        FROM 'C:\SQLData\online_retail.csv'
        WITH (
            FIRSTROW = 2,
            FIELDTERMINATOR = ',',
            TABLOCK
        );

        SET @end_time = GETDATE();

        PRINT 'Load Completed Successfully';
        PRINT '>> Load Duration: ' 
              + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR)
              + ' seconds';
        PRINT '================================================';

    END TRY
    BEGIN CATCH
        PRINT '================================================';
        PRINT 'ERROR OCCURRED DURING BRONZE LOAD PROCESS';
        PRINT 'Error Message : ' + ERROR_MESSAGE();
        PRINT 'Error Number  : ' + CAST(ERROR_NUMBER() AS NVARCHAR);
        PRINT 'Error State   : ' + CAST(ERROR_STATE() AS NVARCHAR);
        PRINT 'Error Line    : ' + CAST(ERROR_LINE() AS NVARCHAR);
        PRINT '================================================';
    END CATCH
END;
GO

-- Execute the procedure
EXEC bronze.proc_load_bronze_online_retail;
GO




