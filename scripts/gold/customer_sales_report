/*
===============================================================================
Gold View: Customer Sales Report (Optimized)
===============================================================================
Purpose:
    Aggregates sales metrics per customer including revenue, orders, and
    assigns a CustomerSegment classification.
===============================================================================
*/

DROP VIEW IF EXISTS gold.customer_sales_report;
GO

CREATE VIEW gold.customer_sales_report AS 

WITH customer_invoice_agg AS (
    SELECT
        InvoiceNo,
        CustomerID,
        Country,
        InvoiceDate,
        SUM(CASE WHEN Quantity > 0 THEN Quantity ELSE 0 END) AS PurchasedQuantity,
        SUM(CASE WHEN Quantity > 0 THEN Quantity * UnitPrice ELSE 0 END) AS GrossRevenue,
        SUM(CASE WHEN Quantity < 0 THEN ABS(Quantity) ELSE 0 END) AS ReturnedQuantity,
        SUM(CASE WHEN Quantity < 0 THEN ABS(Quantity * UnitPrice) ELSE 0 END) AS ReturnRevenue,
        SUM(Quantity) AS NetQuantity,
        SUM(Quantity * UnitPrice) AS NetRevenue
    FROM silver.online_retail_clean
    WHERE CustomerID IS NOT NULL   -- filter out invalid IDs
    GROUP BY InvoiceNo, CustomerID, Country, InvoiceDate
),
customer_agg AS (
    SELECT
        CustomerID,
        Country,
        COUNT(DISTINCT InvoiceNo) AS TotalOrders,
        SUM(PurchasedQuantity) AS TotalQuantityPurchased,
        SUM(ReturnedQuantity) AS TotalQuantityReturned,
        SUM(NetQuantity) AS NetQuantity,
        SUM(GrossRevenue) AS GrossRevenue,
        SUM(ReturnRevenue) AS ReturnRevenue,
        SUM(NetRevenue) AS NetRevenue,
        CAST(SUM(NetRevenue) * 1.0 / NULLIF(COUNT(DISTINCT InvoiceNo),0) AS DECIMAL(10,2)) AS AvgOrderRevenue,
        MIN(InvoiceDate) AS FirstPurchaseDate,
        MAX(InvoiceDate) AS LastPurchaseDate,
        DATEDIFF(DAY, MIN(InvoiceDate), MAX(InvoiceDate)) + 1 AS ActiveDays  -- inclusive
    FROM customer_invoice_agg
    GROUP BY CustomerID, Country
)
SELECT
    CustomerID,
    Country,
    TotalOrders,
    TotalQuantityPurchased,
    TotalQuantityReturned,
    NetQuantity,
    GrossRevenue,
    ReturnRevenue,
    NetRevenue,
    AvgOrderRevenue,
    FirstPurchaseDate,
    LastPurchaseDate,
    ActiveDays,
    CASE
        WHEN NetRevenue < 0 THEN 'Loss Making Customer'
        WHEN NetRevenue >= 10000 AND ActiveDays >= 360 THEN 'High Value Loyal Customer'
        WHEN NetRevenue >= 10000 AND ActiveDays < 360 THEN 'High Value New Customer'
        WHEN NetRevenue BETWEEN 3000 AND 9999 AND ActiveDays >= 180 THEN 'Mid Value Loyal Customer'
        WHEN NetRevenue BETWEEN 3000 AND 9999 AND ActiveDays < 180 THEN 'Mid Value New Customer'
        ELSE 'Low Value Customer'
    END AS CustomerSegment
FROM customer_agg;
GO
